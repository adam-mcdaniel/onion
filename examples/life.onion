


(struct Life (w h)
    (load () {
        (println "Life Started!")
        self.grid = (Collections.map (Collections.range 0 self.w * self.h) (fun (i) (Math.rand_int 0 2)))
        (println "Grid:" self.grid)
        self.time_since_update = 0
        self.has_updated = 0
    })
    (get (x y) {
        (if x >= self.w or x < 0 {
            0
        } {
            (if y >= self.h or y < 0 {
                0
            } {
                self.grid ? x + y * self.w
            })
        })
    })

    (count_neighbors (x y) {
        let count = 0
        count = count + (self.get x - 1 y - 1)
        count = count + (self.get x y - 1)
        count = count + (self.get x + 1 y - 1)
        count = count + (self.get x - 1 y)
        count = count + (self.get x + 1 y)
        count = count + (self.get x - 1 y + 1)
        count = count + (self.get x y + 1)
        count = count + (self.get x + 1 y + 1)
        count
    })
    (update (dt) {
        (if dt > 0.0001 {
            (println "FPS:" (Math.round (1 / dt)))
        })

        self.time_since_update = self.time_since_update + dt
        (if self.time_since_update >= 0.001 {
            self.time_since_update = 0
            self.grid = (Collections.map (Collections.range 0 self.w * self.h) (fun (i) {
                x = i % self.w
                y = i / self.w

                count = (self.count_neighbors x y)
                result = (if (self.get x y) == 1 {
                    (if (count < 2) {
                        0
                    } (if (count > 3) {
                        0
                    } {
                        1
                    }))
                } {
                    (if (count == 3) {
                        1
                    } {
                        0
                    })
                })

                result
            }))
            self.has_updated = 1
        })
    })
    
    (draw () {
        (if self.has_updated {
            (Game.clear 0x000000)
            (Game.draw_text 10 10 "Onion2D Life" 0xFF0000)
            (for i (Collections.range 0 self.w * self.h) {
                x = i % self.w
                y = i / self.w
                (if (self.get x y) == 1 {
                    (Game.rect {x * 8 + 40} {y * 8 + 40} 8 8 0x00FF00)
                })
            })
            self.has_updated = 0
        })
    })
)

(def instance (Life 30 30))

(defun load () {
    (instance.load)
})

(defun update (dt) {
    (instance.update dt)
})

(defun draw () {
    (instance.draw)
})


(Game.run 320 320 "Onion Life")